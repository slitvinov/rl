"${AWK=awk}" '
BEGIN {
    srand(19)
    nwalk = 1
    nset = 1
    lambda = ARGV[1] 
    alpha = ARGV[2]
    ini()
    for (k = 0; k < nset; k++) {
	ini_w()
	compute_w()
	sq[k] = sqrt(rsq(w, r)/count(w))
    }
    printf "%.16e %.16e\n", alpha, sum(sq)/count(sq)
}

function compute_w(   i, j, k, l, x, y, z, P, P1) {
    for (j = 0; j < nwalk; j++) {
	clear(d)
	k = 0
	for (x = 0; !(x in End); x = y) {
	    y = x + rnd()
	    P1 = (y in End) ? End[y] : w[y]
	    z[k] = x
	    P = w[x]
	    for (l = 0; l <= k; l++)
		d[z[l]] += (P1 - P) * lambda^(k - l)
	    k++
	}
	for (i in w)
	    w[i] += alpha * d[i]
    }
}

function ini(   i, j) {
    A = -3
    B =  3
    End[A] = 0
    End[B] = 1
    for (i = A + 1; i < B; i++)
        r[i] = ((j++) + 1)/(B - A)
}

function show(a,    i) {
    for (i = A + 1; i < B; i++) {
        printf "%4.2f ", a[i]
    }
    printf "\n"
}

function rnd() {
    return rand() > 1/2 ? 1 : -1
}

function clear(a) {
    split("", a)
}

function rsq(a, b,  k, s) {
    s = 0
    for (k in a)
	s += (a[k] - b[k])^2
    return s
}

function ini_w(   i) {
    for (i = A + 1; i < B; i++)
        w[i] = 1/2
}

function count(a,   cnt, e) {
    for (e in a)
	cnt++
    return cnt
}

function sum(a,   i, y, c, t, s) { # Kahan summation
    for (i in a) {
        y = a[i] - c
        t = s + y
        c = (t - s) - y
        s = t
    }
    return s
}

' "$@"
