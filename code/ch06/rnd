"${AWK=awk}" -v me=rnd '
BEGIN {
    srand(0)
    ini()
    alpha = ARGV[1]
    nwalk = 5
    nset = 100
    for (k = 0; k < nset; k++) {
        ini_w()
	for (j = 0; j < nwalk; j++) {
	    clear(d)
	    for (x = 0; !(x in End); x = y) {
		y = step(x)
		P = w[x]
		if (y in End)
		    P1 = End[y]
		else
		    P1 = w[y]
		d[x] += P1 - P
	    }
	    for (i in w)
		w[i] += alpha * d[i]
	}
	sq += sqrt(rsq(w, r)/count(w))
    }
    printf "%.16g %.16g\n", alpha, sq/nset
}

function ini(   i, j) {
    A = -3
    B =  3
    End[A] = 0
    End[B] = 1
    for (i = A + 1; i < B; i++)
        r[i] = ((j++) + 1)/(B - A)
}

function show(a,    i) {
    for (i = A + 1; i < B; i++) {
        printf "%4.2f ", a[i]
    }
    printf "\n"
}

function rnd() {
    return rand() > 1/2 ? 1 : -1
}

function step(x) {
    return x + rnd()
}

function clear(a) {
    split("", a)
}

function rsq(a, b,  k, s) {
    s = 0
    for (k in a)
	s += (a[k] - b[k])^2
    return s
}

function ini_w(   i) {
    for (i = A + 1; i < B; i++)
        w[i] = 0.5
}

function count(a,   cnt, e) {
    for (e in a)
	cnt++
    return cnt
}

' "$@"
