"${AWK=awk}" '
BEGIN {
    srand(19)
    nwalk = 5
    nset = 100
    lambda = ARGV[1] 
    alpha = ARGV[2]
    ini()
    for (k = 0; k < nset; k++) {
	ini_w()
	compute_w()
	sq[k] = sqrt(rsq(w, r)/count(w))
    }
    printf "%.16e %.16e\n", alpha, sum(sq)/count(sq)
}

function compute_w(   i, j, l, m, x, y, P, P1, d, e) {
    for (j = 0; j < nwalk; j++) {
	clear(d)
	clear(e)
	for (x = 0; !(x in End); x = y) {
	    y = x + rnd()
	    if (y in End)
		P1 = End[y]
	    else
		P1 = w[y]
	    P = w[x]
	    for (m = A + 1; m < B; m++)		
		d[m] += (P1 - P) * e[m]
	    for (m = A + 1; m < B; m++)
	    	e[m] = lambda * e[m]
	    e[y] = 1
	}
	for (i in w)
	    w[i] += alpha * d[i]
    }
}

function ini(   i, j) {
    A = -3
    B =  3
    End[A] = 0
    End[B] = 1
    for (i = A + 1; i < B; i++)
        r[i] = ((j++) + 1)/(B - A)
}

function show(a,    i) {
    for (i = A + 1; i < B; i++) {
        printf "%4.2f ", a[i]
    }
    printf "\n"
}

function rnd() {
    return rand() > 1/2 ? 1 : -1
}

function clear(a) {
    split("", a)
}

function rsq(a, b,  k, s) {
    s = 0
    for (k in a)
	s += (a[k] - b[k])^2
    return s
}

function ini_w(   i) {
    for (i = A + 1; i < B; i++)
        w[i] = 1/2
}

function count(a,   cnt, e) {
    for (e in a)
	cnt++
    return cnt
}

function sum(a,   i, y, c, t, s) { # Kahan summation
    for (i in a) {
        y = a[i] - c
        t = s + y
        c = (t - s) - y
        s = t
    }
    return s
}

' "$@"
