kill(all);
declare(g, constant);
ratprint: false $

n: 5;
g: 9/10;

act: [ [0, 1], [1, 0], [0, -1], [-1, 0] ];

nxt(x, y, u, v):= block([i, j],
if x = 1 and y = 0 then [10, x, 4]
else if x = 3 and y = 0 then [5, x, 2]
else (
i: x + u,
j: y + v,
if i < 0 or i >= n or j < 0 or j >= n then [-1, x, y] else [0, i, j]));

w[i, j] := block([s, x, y, u, v, r],
s: 0,
for a in act do
(
  [u, v]: a,
  [r, x, y]: nxt(i, j, u, v),
  s: s + 1/4*r + g/4*V[x, y]),
s);

e: [];
a: [];
for j: 0 thru n - 1 do
for i: 0 thru n - 1 do (
  push(V[i, j], a),
  push(w[i, j] - V[i, j], e));

s: linsolve(e, a), numer;

for j: 0 thru n - 1 do (
for i: 0 thru n - 1 do
printf(true, "~5,2f ", ev(V[i, j], s)),
printf(true, "~%"));
